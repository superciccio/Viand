use TodoItem from "./TodoItem.viand"
use State from "./State.viand"

component App:
    $title: string = "Viand Task Manager (Live)"
    $todos: array = []
    $newTodo: string = ""

    fn addTodo():
        if ($newTodo.trim()) {
            # Calling the SQL sibling!
            sql.save($newTodo)
            
            $todos = [...$todos, { text: $newTodo, done: false }]
            $newTodo = ""
        }

    fn loadData():
        $todos = sql.loadAll()

    view:
        div .container:
            h1: State.executiveName + "'s Tasks"
            
            div .input-group:
                input(bind:value: $newTodo, placeholder: "What needs to be done?")
                button -> click(addTodo()): "Add"

            div .list:
                each $todo in $todos:
                    TodoItem(task: $todo.text, done: $todo.done)

    style:
        .container:
            max-width: 400px
            margin: 0 auto
            padding: 20px
            font-family: sans-serif

        .input-group:
            display: flex
            gap: 10px
            margin-bottom: 20px

        input:
            flex: 1
            padding: 8px

test App:
    @ui:
        # Mocking the SQL bridge the Viand way
        set sql.save = vi.fn()
        set sql.loadAll = vi.fn().mockReturnValue([])

        must find "h1" with text "Andrea's Tasks"
        
        # Act
        set $newTodo = "Research Signals"
        click "button" with text "Add"
        
        must find "span" with text "Research Signals"
        must sql.save called with "Research Signals"